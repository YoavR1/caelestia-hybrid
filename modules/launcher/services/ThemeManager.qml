pragma Singleton
pragma ComponentBehavior: Bound

import ".."
import QtQuick
import Quickshell
import Caelestia.Config
import Caelestia.Models
import qs.services
import qs.utils

Searcher {
    id: root

    readonly property string themesDir: Quickshell.shellPath("assets/themes")

    function prettyName(folder: string): string {
        return folder.replace(/[_-]+/g, " ").replace(/\b\w/g, c => c.toUpperCase());
    }

    function transformSearch(search: string): string {
        return search.slice(`${GlobalConfig.launcher.actionPrefix}theme `.length);
    }

    function reload(): void {
        themeDirs.path = "";
        themeDirs.path = root.themesDir;
    }

    list: themes.instances
    useFuzzy: GlobalConfig.launcher.useFuzzy.actions
    keys: ["name", "folder"]
    weights: [0.8, 0.2]

    FileSystemModel {
        id: themeDirs

        path: root.themesDir
        filter: FileSystemModel.Dirs
        watchChanges: true
    }

    Variants {
        id: themes

        model: themeDirs.entries

        Theme {}
    }

    component Theme: QtObject {
        required property FileSystemEntry modelData
        readonly property string folder: modelData?.name ?? ""
        readonly property string name: root.prettyName(folder)
        readonly property string desc: qsTr("Apply theme assets and wallpaper")
        readonly property string wallpaperPath: modelData ? `${modelData.path}/wallpaper.jpg` : ""

        function onClicked(list: AppList): void {
            list.screenState.launcher = false;
            // No explicit save: SettingsFile debounces and persists writes itself since the
            // Phase 2 config rewrite, and ConfigSingleton no longer exposes save().
            GlobalConfig.paths.themeName = folder;
            Wallpapers.setWallpaper(wallpaperPath);
        }
    }
}
